plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'maven'
}

group 'uk.gov.hmcts.reform'
version '0.0.108'

sourceCompatibility = 1.8

repositories {

    mavenLocal()
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }

    jcenter()
    mavenCentral()

    // jitpack should be last resort
    // see: https://github.com/jitpack/jitpack.io/issues/1939
    maven { url 'https://jitpack.io' }
}

// region static code analysis

checkstyle {
    maxWarnings = 0
    toolVersion = '8.10.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.5.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// endregion

// region test coverage

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

// endregion

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-common'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'sscs-common'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/sscs-common'
        version {
            name = project.version
        }
    }
}

dependencies {
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '3.0.1'
    compile group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.2.0'

    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.2.RELEASE'
    compile group: 'io.github.openfeign.form', name: 'feign-form', version: '3.3.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.0'

    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    compile group: 'com.opencsv', name: 'opencsv', version: '4.2'

    compileOnly 'org.projectlombok:lombok:1.16.20'

    testCompile group: 'junit', name: 'junit', version: 4.12
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.7'
    testCompile group: 'org.springframework', name: 'spring-test', version: '5.0.7.RELEASE'


}
