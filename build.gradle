plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '12.1.3'
}

def buildNumber = System.getenv("RELEASE_VERSION") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
// Do not change this version number
version buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1' }
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

checkstyle {
    toolVersion = "11.0.1"
}

pmd {
    toolVersion = "7.16.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = 7.0
    suppressionFile = 'config/owasp/suppressions.xml'

    nvd {
        validForHours = 24
        delay = 8000
        apiKey = System.getenv("NVD_API_KEY")
    }

    analyzers {
        retirejs {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

// endregion

// region test coverage

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

// endregion

// region publishing

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-common'
            version project.version
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

tasks.register('installLocalGitHook', Copy) {
    from new File(rootProject.rootDir, 'config/git/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

compileJava.dependsOn installLocalGitHook

tasks.register('runGitPreCommitTasks') {
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
}

tasks.register('generatePanelCategoryMap', JavaExec) {
    mainClass = "uk.gov.hmcts.reform.sscs.utility.DefaultPanelCompositionParser"
    group = "utility"
    description = "Runs the sscs.utility.DefaultPanelCompositionParser class " +
            "to create panel-category-map.json using CSV_JOHTier_PanelMemberComposition_1.3.csv"
    classpath = sourceSets.main.runtimeClasspath
}

def versions = [
        springboot   : '3.5.5',
        jackson      : '2.19.2',
        junitjupiter : '5.13.4',
        lombok       : '1.18.40'
]

dependencies {

    api group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'

    implementation group: 'org.springframework.boot', name: 'spring-boot', version: versions.springboot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springboot
    implementation group: 'org.springframework', name: 'spring-webmvc', version: '6.2.10'


    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: versions.jackson

    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1', {
        exclude group: 'junit', module: 'junit'
    }

    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.48'

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.18'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.18'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'

    implementation group: 'org.json', name: 'json', version: '20250517'

    implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '8.19.3'

    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.12'
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'
    implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.2.0'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springboot

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'

    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.4'

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.3.0'

    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'

    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.4.1'
    implementation group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.13.55'

    // https://mvnrepository.com/artifact/com.opencsv/opencsv
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.12.0', {
        exclude group: 'commons-collections', module: 'commons-collections'
    }
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.14.0'

    implementation group: 'commons-io', name: 'commons-io', version: '2.20.0'

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.5.8'

    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.5'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springboot
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springboot
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitjupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitjupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junitjupiter
    testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.13.4'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.19.0'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.4'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    testRuntimeOnly group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'
    testRuntimeOnly group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.2.5.Final'
    testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    // CVE-2023-24998
    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.6.0'

    // CVE-2024-25710
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.28.0'

    // CVE-2023-2976, CVE-2020-8908
    checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: checkstyle.toolVersion
    checkstyle group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
    checkstyle group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
}