plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'maven'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
// Do not change this version number
version buildNumber

sourceCompatibility = 1.8

repositories {

    mavenLocal()
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }

    jcenter()
    mavenCentral()

    // jitpack should be last resort
    // see: https://github.com/jitpack/jitpack.io/issues/1939
    maven { url 'https://jitpack.io' }
}

// region static code analysis

checkstyle {
    maxWarnings = 0
    toolVersion = '8.10.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.5.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// endregion

// region test coverage

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

// endregion

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-common'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'sscs-common'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/sscs-common'
        version {
            name = project.version
        }
    }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '5.0.1'
    compile group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: '4.7.0'
    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '8.19'

    compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.0'
    compile group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '3.1.1'

    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
    compile group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.12.6'
    compile group: 'com.opencsv', name: 'opencsv', version: '4.6'
    compile group: 'commons-io', name: 'commons-io', version: '2.7'

    compile group: 'uk.gov.hmcts.reform', name: 'idam-client', version: '1.5.4'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompile group: 'junit', name: 'junit', version: 4.12
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    testCompile group: 'org.springframework', name: 'spring-test', version: '5.2.7.RELEASE'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'

    testCompileOnly 'org.projectlombok:lombok:1.18.10'
    testAnnotationProcessor "org.projectlombok:lombok:1.18.8"
}

