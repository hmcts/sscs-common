plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id "org.owasp.dependencycheck" version "8.2.1"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
// Do not change this version number
version buildNumber

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = 7.0
    suppressionFile = 'config/owasp/suppressions.xml'

    cveValidForHours = 24

    analyzers {
        retirejs {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

// endregion

// region test coverage

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

tasks.withType(Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

// endregion

// region publishing

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-common'
            version project.version
        }
    }
}

task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'config/git/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks')}
    fileMode 0775
}

compileJava.dependsOn installLocalGitHook

task runGitPreCommitTasks {
    dependsOn 'test'
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'dependencyCheckAggregate'
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
}

dependencies {
    api group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    implementation group: 'org.springframework', name: 'spring-aop', version: '5.3.27'
    implementation group: 'org.springframework', name: 'spring-beans', version: '5.3.27'
    implementation group: 'org.springframework.boot', name: 'spring-boot', version: '2.7.12'
    implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: '2.7.12'
    implementation group: 'org.springframework', name: 'spring-context', version: '5.3.27'
    implementation group: 'org.springframework', name: 'spring-expression', version: '5.3.27'
    implementation group: 'org.springframework', name: 'spring-jcl', version: '5.3.27'
    implementation group: 'org.springframework', name: 'spring-webmvc', version: '5.3.27'


    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.15.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.0'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.15.0'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.15.0'

    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1', {
        exclude group: 'junit', module: 'junit'
    }

    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.31'

    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.4.7'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.7'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.7.0'

    implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.14.2', {
        exclude group: 'commons-collections', module: 'commons-collections'
    }

    implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.9'

    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.1'
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'

    implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.0.1'

    implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.8.6', {
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter-openfeign'
    }

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3', {
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter-openfeign'
    }

    implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.1.1', {
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter-openfeign'
    }

    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.4', {
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter'
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-commons'
    }

    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'

    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.3'
    implementation group: 'com.googlecode.libphonenumber', name: 'libphonenumber', version: '8.13.11'

    // https://mvnrepository.com/artifact/com.opencsv/opencsv
    implementation group: 'com.opencsv', name: 'opencsv', version: '5.7.1', {
        exclude group: 'commons-collections', module: 'commons-collections'
    }
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'

    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation group: "com.networknt", name: "json-schema-validator", version: "1.0.80"

    implementation group: "org.projectlombok", name: "lombok", version: "1.18.26"
    annotationProcessor group: "org.projectlombok", name: "lombok", version: "1.18.26"

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "2.7.12"
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: "2.7.12"

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.3'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.9.3'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.9.3'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.9.3'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.3.1'
    testImplementation group: 'org.springframework', name: 'spring-test', version: '5.3.27'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    testRuntimeOnly group: 'org.glassfish', name: 'jakarta.el', version: '3.0.4'
    testRuntimeOnly group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.2.5.Final'
    testImplementation group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
}

